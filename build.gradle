plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.2.51'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC8"
}

group 'com.virgingates'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'io.vertx.core.Launcher'
def mainVerticleName = 'com.virgingates.verticles.MainVerticle'

apply plugin: "jacoco"

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    compile group: 'io.vertx', name: 'vertx-web', version: '3.5.3'
    compile group: 'io.vertx', name: 'vertx-lang-kotlin', version: '3.5.3'
    compile group: 'io.vertx', name: 'vertx-lang-kotlin-coroutines', version: '3.5.3'
    compile group: 'io.vertx', name: 'vertx-web-client', version: '3.5.3'
    compile group: 'io.vertx', name: 'vertx-service-proxy', version: '3.5.3'
    compile group: 'io.vertx', name: 'vertx-codegen', version: '3.5.3'

    compile group: 'com.google.api-client', name: 'google-api-client', version: '1.23.0'
    compile group: 'com.google.oauth-client', name: 'google-oauth-client-jetty', version: '1.23.0'
    compile group: 'com.google.apis', name: 'google-api-services-sheets', version: 'v4-rev516-1.23.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'io.vertx', name: 'vertx-unit', version: '3.5.3'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.20.1'
}

build.dependsOn jacocoTestReport
build.dependsOn clean
build.dependsOn detektCheck

check.dependsOn task('checkDuplicate')
check.dependsOn jacocoTestCoverageVerification

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes 'Main-Verticle': mainVerticleName
        attributes 'Main-Class': mainClassName
    }


    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

detekt {
    version = "1.0.0.RC8"
    defaultProfile {
        input = file("src/main/kotlin")
        filters = ".*/resources/.*,.*/build/.*"
        config = "$project.rootDir/detekt-config.yml"
        output = "${project.buildDir}/reports/detekt.xml"
        parallel = true
    }
}

checkDuplicate {
    doLast {
        def process = "java -jar ./simian-2.5.10.jar -language=java -excludes=/test/ **/*.kt".execute()
        process.waitForProcessOutput(System.out, System.err)
        if (process.exitValue() == 1) {
            throw new Exception("Duplicate Found")
        }
    }
}

jacocoTestReport {
    group = "Reporting"
    reports {
        html.enabled true
        xml.enabled true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.6
            }
        }
    }
}
